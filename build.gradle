apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

ext.rxAndroidVersion = "2.0.1"
ext.rxJavaVersion = "2.1.0"         // for explicit dependency needed for rxAndroid

ext.firebaseVersion="10.2.6"
ext.firebaseUi="1.2.0"              // use firebase 10.2.4, gps-auth 10.2.0

ext.supportVersion="26.0.0-beta1"
ext.a4sSdkVersionName="4.0.0"

ext.samplesVersionCode=03
ext.samplesVersionName="0.3"

android {
    compileSdkVersion "android-O"
    buildToolsVersion "26.0.0-rc2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 'O'
        versionCode samplesVersionCode
        versionName samplesVersionName
    }

    flavorDimensions "env"
    productFlavors {
        prod {
            dimension "env"
        }
        preprod {
            dimension "env"
        }
        dev {
            dimension "env"
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    include '**/*.java'
    failOnError=false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

dependencies {
    compile 'com.jakewharton:butterknife:8.6.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.6.0'

    compile 'com.android.support:design:'+supportVersion
    compile 'com.android.support:support-v4:'+supportVersion
    compile 'com.android.support:appcompat-v7:'+supportVersion

    compile "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    compile "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    compile "com.google.firebase:firebase-auth:$firebaseVersion"
    compile "com.google.firebase:firebase-analytics-impl:$firebaseVersion"
    compile "com.google.firebase:firebase-database:$firebaseVersion"

    compile "com.firebaseui:firebase-ui-auth:$firebaseUi"
    compile "com.firebaseui:firebase-ui-database:$firebaseUi"

    compile 'com.github.bumptech.glide:glide:3.7.0'

    //debugCompile group: 'com.ad4screen.sdk', name: 'A4SSDK', version: "SNAPSHOT-$a4sSdkVersionName", changing: true
    //releaseCompile 'com.ad4screen.sdk:A4SSDK:'+a4sSdkVersionName

    api project(':A4SSDK-src')
}

publishing.repositories {
    maven {
        name 'Bintray'
        url 'https://api.bintray.com/maven/accengage/maven/A4SSamples'
        credentials {
            username = bintrayUsername
            password = bintrayPassword
        }
    }
}

publishing.publications {
    dev(MavenPublication) {
        groupId 'com.accengage.samples'
        artifactId 'A4SSamples'
        version 'SNAPSHOT-'+samplesVersionName
        artifact 'build/outputs/aar/A4SSamples-debug.aar'
        artifact androidSourcesJar
        artifact androidJavadocsJar
    }
    prod(MavenPublication) {
        groupId 'com.accengage.samples'
        artifactId 'A4SSamples'
        version samplesVersionName
        artifact 'build/outputs/aar/A4SSamples-release.aar'
        artifact androidSourcesJar
        artifact androidJavadocsJar

        pom.withXml {
            def dependenciesNode = asNode().appendNode('dependencies')
            def sdkDependencyNode = dependenciesNode.appendNode('dependency')
            sdkDependencyNode.appendNode('groupId', 'com.ad4screen.sdk')
            sdkDependencyNode.appendNode('artifactId', 'A4SSDK')
            sdkDependencyNode.appendNode('version', a4sSdkVersionName)
            sdkDependencyNode.appendNode('scope', 'compile')
            sdkDependencyNode.appendNode('type', 'aar')
            def supportV4Node = dependenciesNode.appendNode('dependency')
            supportV4Node.appendNode('groupId', 'com.android.support')
            supportV4Node.appendNode('artifactId', 'support-v4')
            supportV4Node.appendNode('version', supportVersion)
            supportV4Node.appendNode('scope', 'compile')
            supportV4Node.appendNode('type', 'aar')
            def appCompatV7Node = dependenciesNode.appendNode('dependency')
            appCompatV7Node.appendNode('groupId', 'com.android.support')
            appCompatV7Node.appendNode('artifactId', 'appcompat-v7')
            appCompatV7Node.appendNode('version', supportVersion)
            appCompatV7Node.appendNode('scope', 'compile')
            appCompatV7Node.appendNode('type', 'aar')
        }
    }
}
