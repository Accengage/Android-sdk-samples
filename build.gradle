apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 24
    buildToolsVersion "23.0.3"

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    include '**/*.java'
    failOnError=false
}

task androidJavadocsJar(type: Jar, dependsOn: androidJavadocs) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

dependencies {
    compile 'com.android.support:support-v4:24.2.0'
    compile 'com.android.support:appcompat-v7:24.2.0'
    compile 'com.a4s.sdk:usea4s-annotation:1.0.1'

    compile group: 'com.ad4screen.sdk', name: 'A4SSDK', version: 'SNAPSHOT', changing: true
    //compile 'com.ad4screen.sdk:A4SSDK:3.5.0.1'

}

publishing.repositories {
    maven {
        name 'Bintray'
        url 'https://api.bintray.com/maven/accengage/maven/A4SSamples'
        credentials {
            username = bintrayUsername
            password = bintrayPassword
        }
    }
}

publishing.publications {
    dev(MavenPublication) {
        groupId 'com.accengage.samples'
        artifactId 'A4SSamples'
        version 'SNAPSHOT'
        artifact 'build/outputs/aar/A4SSamples-debug.aar'
        artifact androidSourcesJar
        artifact androidJavadocsJar
    }
    prod(MavenPublication) {
        groupId 'com.accengage.samples'
        artifactId 'A4SSamples'
        version sdkVersionName
        artifact 'build/outputs/aar/A4SSamples-release.aar'
        artifact androidSourcesJar
        artifact androidJavadocsJar

        pom.withXml {
            def dependenciesNode = asNode().appendNode('dependencies')
            def sdkDependencyNode = dependenciesNode.appendNode('dependency')
            sdkDependencyNode.appendNode('groupId', 'com.ad4screen.sdk')
            sdkDependencyNode.appendNode('artifactId', 'A4SSDK')
            sdkDependencyNode.appendNode('version', "3.5.0.1") // sdkVersionName TODO
            sdkDependencyNode.appendNode('scope', 'compile')
            sdkDependencyNode.appendNode('type', 'aar')
            def supportV4Node = dependenciesNode.appendNode('dependency')
            supportV4Node.appendNode('groupId', 'com.android.support')
            supportV4Node.appendNode('artifactId', 'support-v4')
            supportV4Node.appendNode('version', '24.2.0')
            supportV4Node.appendNode('scope', 'compile')
            supportV4Node.appendNode('type', 'aar')
            def appCompatV7Node = dependenciesNode.appendNode('dependency')
            appCompatV7Node.appendNode('groupId', 'com.android.support')
            appCompatV7Node.appendNode('artifactId', 'appcompat-v7')
            appCompatV7Node.appendNode('version', '24.2.0')
            appCompatV7Node.appendNode('scope', 'compile')
            appCompatV7Node.appendNode('type', 'aar')
            def usesA4SNode = dependenciesNode.appendNode('dependency')
            usesA4SNode.appendNode('groupId', 'com.a4s.sdk')
            usesA4SNode.appendNode('artifactId', 'usea4s-annotation')
            usesA4SNode.appendNode('version', '1.0.1')
            usesA4SNode.appendNode('scope', 'compile')
            usesA4SNode.appendNode('type', 'aar')
        }
    }
}
